programa
{	
	/* Exercício 068 
	 *  - Faça um campo minado em uma matriz de 5x5.
	 *  Felipe Rocha
	 */
	 
	inclua biblioteca Util --> u
	funcao inicio()
	{
		caracter jogo[5][5]	
		
		escreva("{ EXERCÍCIO 068 - Jogo Campo Minado }\n")

		para(inteiro l = 0; l < u.numero_linhas(jogo); l++){
			para(inteiro c = 0; c < u.numero_colunas(jogo); c++){
				jogo[l][c] = '-'
			}
		}

		// Sorteando bombas
		inteiro quant = 5, pL, pC, bomba = 0
		
		enquanto(bomba < quant){
			pL = sorteia(0, u.numero_linhas(jogo) - 1)
			pC = sorteia(0, u.numero_colunas(jogo) - 1)
			se(jogo[pL][pC] == '-'){
				jogo[pL][pC] = 'O'
				bomba++
			}
		}
		// Iniciar o jogo
		inteiro total = 5, tentativa = 1, pontos = 0, lin, col
		logico bum = falso
		enquanto (tentativa < total ou pontos < total*2){
			// Mostrar tabuleiro com ?????
			escreva("=======================================\n")
			para(inteiro l = 0; l < u.numero_linhas(jogo); l++){
				para(inteiro c = 0; c < u.numero_colunas(jogo); c++){
					se (jogo[l][c] == '-' ou jogo[l][c] == 'O'){
						escreva("? ")
					} senao {
						escreva(jogo[l][c] , " ")
					}
					u.aguarde(50)
				}
				escreva("\n")
			}
			// Jogador joga
			escreva("=======================================\n")
			escreva("Faça a sua jogada! (Tentativa: " , tentativa , ")\n")
			faca{
				escreva("Linha = ")
				leia(lin)
			} enquanto (lin >= u.numero_linhas(jogo))
			faca{
				escreva("Coluna = ")
				leia(col)
			} enquanto (col >= u.numero_colunas(jogo))
			se (jogo[lin][col] == 'O'){
				escreva("--> TIRO ERRADO! Você acertou uma BOMBA!\n")
				bum = verdadeiro
				jogo[lin][col] = '*'
				pare
			} senao se (jogo[lin][col] == '-'){
				escreva("--> TIRO CERTO! Parabéns!\n")
				jogo[lin][col] = 'X'
				tentativa++
				pontos += 2
				
			} senao se (jogo[lin][col] == 'X'){
				escreva("--> Você já atirou nessa posição! Tente novamente!\n")
			}
			u.aguarde(1000)
		}
		// Fim do jogo
		escreva("=======================================\n")
		escreva("GAME OVER!\n")
		escreva("=======================================\n")
		// Mostrar tabuleiro completo
		para(inteiro l = 0; l < u.numero_linhas(jogo); l++){
			para(inteiro c = 0; c < u.numero_colunas(jogo); c++){
				escreva(jogo[l][c] , " ")
				u.aguarde(200)
			}
			escreva("\n")
		}
		escreva("=======================================\n")
		se (bum){
			escreva("VOCÊ NÃO GANHOU!\n")
		}senao {
			escreva("VITÓRIA! VOCÊ GANHOU!!\n")
		}
		escreva("=======================================\n")
		u.aguarde(1000)
		escreva("Você fez " , pontos , " pontos em " , (tentativa - 1) , " tentativas!\n")
		escreva("=======================================\n\n")
				
	}
}	